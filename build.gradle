plugins {
    id 'com.gradle.plugin-publish' version '0.9.2'
    id 'com.jfrog.bintray' version '1.5'
    id 'com.palantir.git-version' version '0.4.0'
    id 'eclipse'
    id 'groovy'
    id 'idea'
    id 'maven-publish'
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()

    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile files(createClasspathManifest)
}

group 'com.palantir.gradle.javadist'
version gitVersion()

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}
  
publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar) {
                classifier 'sources'
            }
        }
    }
}
  
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'releases'
        name = 'gradle-java-distribution'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = ['bintray']
    }
}
  
bintrayUpload.onlyIf {
    System.getenv('TRAVIS_TAG') && System.getenv('BINTRAY_USER') && System.getenv('BINTRAY_KEY') && project.version ==~ /\d+\.\d+\.\d+/
}
  
bintrayUpload.dependsOn { generatePomFileForBintrayPublication }
bintrayUpload.dependsOn { sourceJar }
bintrayUpload.dependsOn { build }
pluginBundle {
    website = 'https://github.com/palantir/gradle-java-distribution'
    vcsUrl = 'https://github.com/palantir/gradle-java-distribution'
    description = 'Creates a distributable package containing libraries and scripts necessary to easily deploy and run a Java project.'
    tags = ['distribution', 'application']

    plugins {
        dockerPlugin {
            id = 'com.palantir.java-distribution'
            displayName = 'Palantir Java Distribution'
        }
    }
}

publishPlugins.onlyIf {
    System.getenv('TRAVIS_TAG') && project.version ==~ /\d+\.\d+\.\d+/
}
